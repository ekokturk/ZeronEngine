# Add a new Zeron Module
macro(zeron_add_module module_name module_dir)
    message("ZERON -- Module: ${module_name}")
    if(NOT EXISTS ${module_dir})
        message(FATAL_ERROR "${ZERON_ERROR_MSG} Zeron Third Party directory doesn't exist!")
    endif()
    file(GLOB_RECURSE module_files	"${module_dir}/*.cpp"	"${module_dir}/*.h")
    source_group(TREE "${module_dir}"	PREFIX "Source Files/" FILES ${module_files})
    add_library(${module_name} ${module_files})
    set_target_properties(${module_name} PROPERTIES FOLDER "ZeronEngine")
    set_property(TARGET ${module_name} PROPERTY VS_DEBUGGER_WORKING_DIRECTORY $<TARGET_FILE_DIR:${module_name}>)
endmacro()

# Add a new Zeron Third Party dependency
macro(zeron_add_module_dependency dep_name)
    if(NOT DEFINED ZERON_THIRDPARTY_DIR)
        message(FATAL_ERROR "${ZERON_ERROR_MSG} Zeron Third Party directory is not defined!")
        if(NOT EXISTS ${ZERON_THIRDPARTY_DIR})
            message(FATAL_ERROR "${ZERON_ERROR_MSG} Zeron Third Party directory doesn't exist!")
        endif()
    endif()
    # Only create target if it's not already created
    if(NOT TARGET ${dep_name})
        message("ZERON ------ Dependency: ${dep_name}")
        file(RELATIVE_PATH rel_path ${CMAKE_CURRENT_SOURCE_DIR} ${ZERON_THIRDPARTY_DIR})
        add_subdirectory("${rel_path}/${dep_name}" "${CMAKE_CURRENT_BINARY_DIR}/${dep_name}")
    endif()
endmacro()

# Create a new Zeron Test Module
macro(zeron_add_module_test test_name)
    include("${ZERON_CMAKE_UTILS}/ZeronProjectUtils.cmake")
    file(GLOB_RECURSE test_files LIST_DIRECTORIES false *.h *.cpp)
    zeron_set_output_directory("Tests/${test_name}")
    add_executable(${test_name} ${test_files})
    add_test(NAME ${test_name} COMMAND ${test_name})
    set_target_properties(${test_name} PROPERTIES FOLDER "ZeronEngine/Tests")
    zeron_copy_runtime_dependencies(${test_name})
endmacro()
