macro(zeron_set_output_directory dirName)
    if(NOT DEFINED ${ZERON_BINARY_OUTPUT_DIRECTORY})
        set(OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/Binaries")
    endif()
    set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG ${OUTPUT_DIRECTORY}/${dirName}/Debug/)
    set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_DEBUG ${OUTPUT_DIRECTORY}/${dirName}/Debug/)
    set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_DEBUG ${OUTPUT_DIRECTORY}/${dirName}/Debug/)
    
    set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE ${OUTPUT_DIRECTORY}/${dirName}/Release/)
    set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_RELEASE ${OUTPUT_DIRECTORY}/${dirName}/Release/)
    set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_RELEASE ${OUTPUT_DIRECTORY}/${dirName}/Release/)
endmacro()

macro(zeron_copy_runtime_dependencies target)
    if(WIN32)
        add_custom_command(TARGET ${target} POST_BUILD
            COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_RUNTIME_DLLS:${target}> $<TARGET_FILE_DIR:${target}>
            COMMAND_EXPAND_LISTS
        )
    endif()
endmacro()

macro(zeron_add_module module_name module_dir)
    message("ZERON -- Module: ${module_name}")
    if(NOT EXISTS ${module_dir})
        message(FATAL_ERROR "Zeron Third Party directory doesn't exist!")
    endif()
    file(GLOB_RECURSE module_files	"${module_dir}/*.cpp"	"${module_dir}/*.h")
    source_group(TREE "${module_dir}"	PREFIX "Source Files/" FILES ${module_files})
    add_library(${module_name} ${module_files})
    set_target_properties(${module_name} PROPERTIES FOLDER "ZeronEngine")
    set_property(TARGET ${module_name} PROPERTY VS_DEBUGGER_WORKING_DIRECTORY $<TARGET_FILE_DIR:${module_name}>)
endmacro()

macro(zeron_add_module_dependency dep_name)
    if(NOT DEFINED ZERON_THIRDPARTY_DIR)
        message(FATAL_ERROR "Zeron Third Party directory is not defined!")
        if(NOT EXISTS ${ZERON_THIRDPARTY_DIR})
            message(FATAL_ERROR "Zeron Third Party directory doesn't exist!")
        endif()
    endif()
    message("ZERON ------ Dependency: ${dep_name}")
    file(RELATIVE_PATH rel_path ${CMAKE_CURRENT_SOURCE_DIR} ${ZERON_THIRDPARTY_DIR})
    add_subdirectory("${rel_path}/${dep_name}" "${CMAKE_CURRENT_BINARY_DIR}/${dep_name}")
endmacro()

macro(zeron_add_module_test test_name)
    file(GLOB_RECURSE test_files LIST_DIRECTORIES false *.h *.cpp)
    zeron_set_output_directory("Tests/${test_name}")
    add_executable(${test_name} ${test_files})
    add_test(NAME ${test_name} COMMAND ${test_name})
    set_target_properties(${test_name} PROPERTIES FOLDER "ZeronEngine/Tests")
    zeron_copy_runtime_dependencies(${test_name})
endmacro()

macro(zeron_add_shader_directory newShaderDir)
    if(NOT EXISTS ${newShaderDir})
        message(FATAL_ERROR "Shader directory '${newShaderDir} was not found'!")
    endif()
    get_property(_shaderDirs GLOBAL PROPERTY ZERON_SHADER_DIRECTORIES)
    set_property(GLOBAL PROPERTY ZERON_SHADER_DIRECTORIES ${_shaderDirs} ${newShaderDir})
endmacro()